// Licensed to the Apache Software Foundation (ASF) under one
// or more contributor license agreements.  See the NOTICE file
// distributed with this work for additional information
// regarding copyright ownership.  The ASF licenses this file
// to you under the Apache License, Version 2.0 (the
// "License"); you may not use this file except in compliance
// with the License.  You may obtain a copy of the License at
//
//   http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

// This file contains common tasks and configuration for artifact publishing.

apply plugin: "maven"
apply plugin: "signing"

// Only sign artifacts if skipSigning is false,
// the version is not a snapshot, and we are uploading them to maven.
// This allows simplified builds and local maven installs.
def skipSigning = propertyExists("skipSigning")
def shouldSign = !skipSigning && !version.endsWith("SNAPSHOT") && project.gradle.startParameter.taskNames.any { it.contains("upload") }
def mavenUrl = propertyWithDefault("mavenUrl", "")
def mavenUsername = propertyWithDefault("mavenUsername", "")
def mavenPassword = propertyWithDefault("mavenPassword", "")

uploadArchives {
  repositories {
    signing {
      required { shouldSign }
      sign configurations.archives
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        repository(url: "${mavenUrl}") {
          authentication(userName: "${mavenUsername}", password: "${mavenPassword}")
        }
      }
    }
  }
}

// Add the install task to the "Upload" group so it's visible in the tasks output.
install.group = "Upload"

// Sort the generated maven dependencies to make pom comparisons easier.
tasks.withType(Upload) {
  def installer = install.repositories.mavenInstaller
  def deployer = uploadArchives.repositories.mavenDeployer

  [installer, deployer]*.pom*.whenConfigured { pom ->
    pom.dependencies = pom.dependencies.sort { dep ->
      "$dep.scope:$dep.optional:$dep.groupId:$dep.artifactId"
    }
  }
}


